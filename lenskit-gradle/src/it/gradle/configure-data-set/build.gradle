buildscript {
    repositories {
        maven {
            url "$project.testRepoURI"
        }
        mavenCentral()
    }
    dependencies {
        classpath 'org.hamcrest:hamcrest-library:1.3'
        classpath "org.lenskit:lenskit-gradle:$project.lenskitVersion"
    }
}

import org.lenskit.gradle.*
import org.lenskit.specs.*
import org.lenskit.specs.data.*
import org.lenskit.specs.eval.*

import static org.hamcrest.MatcherAssert.assertThat
import static org.hamcrest.Matchers.*

apply plugin: 'java'
apply plugin: 'lenskit'

task evaluate(type: TrainTest) {
    dryRun true
    dataSet {
        name 'Foo'
        trainSource textFile('train.csv')
        testSource textFile('test.csv')
        attributes Partition: 1
    }
}

task verifyEvalOutput(dependsOn: evaluate) {
    doLast {
        def specFile = file("$buildDir/evaluate-spec.json")
        assertThat("spec file exists", specFile.exists())
        def exp = SpecUtils.load(TrainTestExperimentSpec, specFile.toPath())
        assertThat(exp.dataSets, hasSize(1))
        def spec = exp.dataSets[0]
        assertThat(spec.name, equalTo('Foo'))
        assertThat(spec.attributes, hasEntry('Partition', 1))
        assertThat(spec.testSource, instanceOf(TextDataSourceSpec))
    }
}

check.dependsOn verifyEvalOutput