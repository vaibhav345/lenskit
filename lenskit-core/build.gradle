/*
 * Build system for LensKit, and open-source recommender systems toolkit.
 * Copyright 2010-2014 Regents of the University of Minnesota and contributors
 * Work on LensKit has been funded by the National Science Foundation under
 * grants IIS 05-34939, 08-08692, 08-12148, and 10-17697.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions
 * are met:
 *
 * - Redistributions of source code must retain the above copyright
 *   notice, this list of conditions and the following disclaimer.
 *
 * - Redistributions in binary form must reproduce the above copyright
 *   notice, this list of conditions and the following disclaimer in the
 *   documentation and/or other materials provided with the
 *   distribution.
 *
 * - Neither the name of the University of Minnesota nor the names of
 *   its contributors may be used to endorse or promote products derived
 *   from this software without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
 * "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
 * LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
 * A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
 * HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
 * SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
 * LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
 * DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
 * THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 * (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
 * OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

apply from: "$rootDir/gradle/maven.gradle"
apply plugin: 'groovy'
apply from: "$rootDir/gradle/test-utils.gradle"

dependencies {
    compileOnly group: 'com.google.auto.service', name: 'auto-service', version: '1.0-rc2'

    compile project(':lenskit-api')

    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.12'
    compile group: 'net.jcip', name: 'jcip-annotations', version: '1.0'
    compile group: 'it.unimi.dsi', name:'fastutil', version: '8.1.0'
    compile group: 'org.grouplens.grapht', name: 'grapht', version: '0.11.0-SNAPSHOT'
    compile group: 'com.google.guava', name: 'guava', version: guavaVersion
    compile group: 'org.joda', name: 'joda-convert', version: '1.8.1'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.7.4'
    compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: '2.7.4'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'
    compile group:'org.apache.commons', name:'commons-math3', version:'3.6.1'
    compile group:'org.ow2.asm', name:'asm-tree', version:'5.2'

    compile group: 'org.apache.commons', name: 'commons-compress', version: '1.12'

    runtime group: 'org.tukaani', name: 'xz', version: '1.5'

    testRuntime group: 'com.h2database', name: 'h2', version: '1.4.192'
}

meta {
    name 'LensKit Core'
    description 'The core of LensKit, providing basic implementations and algorithm support.'
}

/* Since we define an annotation processor in this project, run it in its own task */
task checkAnnotations(type: JavaCompile, dependsOn: classes) {
    source sourceSets.main.java
    classpath = sourceSets.main.compileClasspath
    destinationDir = file("$buildDir/check-output")

    options.fork = false
    options.compilerArgs << '-proc:only'
}
check.dependsOn checkAnnotations

// Make LensKit version info available
task versionInfo {
    mustRunAfter processResources
    ext.versionFile = {
        "$sourceSets.main.output.resourcesDir/META-INF/lenskit/version.properties"
    }
    outputs.file versionFile
    inputs.property 'version', version
    doLast {
        def vf = file(versionFile)
        mkdir vf.parentFile
        vf.text = "lenskit.version=$version\n"
    }
}
classes.dependsOn versionInfo

/* Generate the Git commit list */
task listGitCommits(group: 'build') {
    // where does the report go?
    ext.outputFile = "$sourceSets.main.output.resourcesDir/META-INF/lenskit/git-commits.lst"
    // make sure we don't get stomped on
    mustRunAfter processResources
    
    outputs.file outputFile
    // we change with the HEAD revision
    inputs.property('HEAD') { git.headRevision.name }

    doLast {
        mkdir file(outputFile).parentFile
        git.withRepo { repo, cmd ->
            logger.info 'git repository at {}', repo.directory
            def head = repo.resolve('HEAD')
            logger.info 'head revision: {}', head.name
            file(outputFile).withPrintWriter { out ->
                int n = 0
                for (commit in cmd.log().add(git.headRevision).call()) {
                    logger.debug 'found {}', commit.id
                    out.println commit.id.name
                    n += 1
                }
                logger.info 'listed {} revisions', n
            }
        }
    }
}
classes.dependsOn listGitCommits
