/* This script tests the JVM argument logic.  It consists of three pieces:
 *
 * 1. A gradle.properties file that specifies a JVM argument via the 'lenskit.jvmArgs' property.
 * 2. A Groovy script that checks for this file's existence.
 * 3. This file, that runs the script using LensKit's run-script CLI command.
 */

buildscript {
    repositories {
        maven {
            url "$project.testRepoURI"
        }
        mavenCentral()
    }
    dependencies {
        classpath 'org.hamcrest:hamcrest-library:1.3'
        classpath "org.lenskit:lenskit-gradle:$project.lenskitVersion"
    }
}

import org.lenskit.gradle.*
import static org.hamcrest.MatcherAssert.*

apply plugin: 'java'
apply plugin: 'lenskit'
apply from: 'common.gradle'

task checkScript(type: LenskitExec) {
    logger.lifecycle "jvm args: $lenskit.jvmArgs"
    logFile "$buildDir/run-script.log"
    command 'run-script'
    args file('verify.groovy')
    doFirst {
        mkdir buildDir
        logger.lifecycle "my jvm args: ${checkScript.jvmArgs}"
    }
}

check {
    dependsOn 'checkScript'
    doLast {
        assertThat "log file exists", file("$buildDir/run-script.log").exists()
    }
}
