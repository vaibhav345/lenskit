buildscript {
    repositories {
        maven {
            url project.testRepoURI
        }
        mavenCentral()
    }
    dependencies {
        classpath "org.lenskit:lenskit-gradle:$project.lenskitVersion"
    }
}

import org.lenskit.gradle.*

apply plugin: 'java'
apply plugin: 'lenskit'
apply from: 'common.gradle'

dependencies {
    runtime 'org.hamcrest:hamcrest-library:1.3'
    runtime "org.lenskit:lenskit-test:$project.lenskitVersion"
    testRuntime 'com.xlson.groovycsv:groovycsv:1.0'
}

task crossfold(type: Crossfold) {
    input textFile {
        file ratingsFile
        delimiter "\t"
    }
    outputFormat "CSV_GZIP"
    partitionCount 5
    userPartitionMethod holdout(5)
    includeTimestamps false
}

task trainTest(type: TrainTest) {
    maxMemory '256m'
    logFile "train-test.log"
    logFileLevel 'DEBUG'
    dataSet crossfold //, isolate: true
    cacheDirectory 'cache'
    outputFile 'results.csv'
    userOutputFile 'users.csv'
    algorithm 'Baseline', 'baseline.groovy'
    predict {
        metric 'coverage'
        metric 'rmse'
        metric('ndcg') {
            columnName 'PredRankAcc'
        }
    }
    recommend {
        listSize 10
        metric 'length'
        metric 'entropy'
        metric('ndcg') {
            columnName 'nDCG'
        }
        metric 'popularity'
        metric 'mrr'
        metric 'map'
        metric 'pr'
    }
}

task runVerifyScript(type: JavaExec) {
    dependsOn trainTest
    classpath configurations.testRuntime

    main 'groovy.ui.GroovyMain'
    args 'verify.groovy'
}

check.dependsOn runVerifyScript
